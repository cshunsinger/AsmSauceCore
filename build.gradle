plugins {
    id "io.freefair.lombok" version "5.3.0"
    id 'idea'
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'jacoco'
    id "org.owasp.dependencycheck" version "6.1.5"
}

group = 'com.chunsinger.asmsauce'
version = '0.1-SNAPSHOT'
description = """A Java library which provides a high-level api for dynamically writing new classes at runtime, loading them, and using them."""

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.maven.apache.org/maven2" }
}

test {
    jvmArgs '--enable-preview'
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

if (project.hasProperty('build_version')) {
    project.version = project.build_version
} else {
    project.version = 'dev'
}

def junit_jupiter = '5.7.0'

dependencies {
    implementation group: 'org.ow2.asm', name: 'asm', version: '9.1'

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'

    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junit_jupiter
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit_jupiter
    testImplementation group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.5.2'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.5.13'
}

check.finalizedBy jacocoTestReport

task getProjectGroup {
    doLast {
        println project.group
    }
}
task getProjectDescription {
    doLast {
        println project.description
    }
}